define %i 0;
define %j 0;
define %k 0;
define %l 0;
define %n 40;
define %xc %width/2;
define %yc %height/2;
define %d 0.8;
define %ibond 0;
while (%i < %n);

  // lay down the atoms
  add atom 0 (%xc - (%n/2-%i)*%d, %yc-%d);
  add atom 0 (%xc - (%n/2-%i)*%d, %yc);
  add atom 0 (%xc - (%n/2-%i)*%d, %yc+%d);

  // vertical radial bonds
  set %j %i*3;
  set %k %i*3+1;
  set %l %i*3+2;
  set %ibond %number_of_rbonds;
  bond %j %k 1;
  if(%i>0 and %i<%n-1);
    set rbond[%ibond].style GHOST_STYLE;
  else;
    set rbond[%ibond].style SOLID_LINE_STYLE;
  endif;
  set %ibond %number_of_rbonds;
  bond %k %l 1;
  if(%i>0 and %i<%n-1);
    set rbond[%ibond].style GHOST_STYLE;
  else;
    set rbond[%ibond].style SOLID_LINE_STYLE;
  endif;

  if(%i>0);

    set %ibond %number_of_rbonds;
    set %j %i*3;
    set %k (%i-1)*3;
    bond %j %k 1;
    set rbond[%ibond].style SOLID_LINE_STYLE;

    set %ibond %number_of_rbonds;
    set %j %i*3+1;
    set %k (%i-1)*3+1;
    bond %j %k 1;
    set rbond[%ibond].style GHOST_STYLE;

    set %ibond %number_of_rbonds;
    set %j %i*3+2;
    set %k (%i-1)*3+2;
    bond %j %k 1;
    set rbond[%ibond].style SOLID_LINE_STYLE;

    // 1 angular bond
    set %j 3*%i;
    set %k 3*(%i-1);
    set %l 3*(%i-1)+1;
    bond %j %k %l 100;

    // 2 angular bond
    set %j 3*(%i-1);
    set %k 3*%i;
    set %l 3*%i+1;
    bond %j %k %l 100;

    // 3 angular bond
    set %j 3*%i;
    set %k 3*%i+1;
    set %l 3*(%i-1)+1;
    bond %j %k %l 100;

    // 4 angular bond
    set %j 3*(%i-1);
    set %k 3*(%i-1)+1;
    set %l 3*%i+1;
    bond %j %k %l 100;

    // 5 angular bond
    set %j 3*(%i-1)+1;
    set %k 3*%i+1;
    set %l 3*%i+2;
    bond %j %k %l 100;

    // 6 angular bond
    set %j 3*(%i-1)+2;
    set %k 3*(%i-1)+1;
    set %l 3*%i+1;
    bond %j %k %l 100;

    // 7 angular bond
    set %j 3*(%i-1)+1;
    set %k 3*(%i-1)+2;
    set %l 3*%i+2;
    bond %j %k %l 100;

    // 8 angular bond
    set %j 3*(%i-1)+2;
    set %k 3*%i+2;
    set %l 3*%i+1;
    bond %j %k %l 100;

  endif;

  %i++;

endwhile;

// hide particles
select atom all;
set visible off;

// clear selections
select atom none;
select rbond none;
select abond none;